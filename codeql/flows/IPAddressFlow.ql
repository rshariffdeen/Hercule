/**
 * @name Detects IP Address Flow
 * @description detects IP address used in Network calls
 * @Author Ridwan Shariffdeen
 * @kind problem
 * @id py/ip-address-flow
 * @security-severity 4.0
 * @problem.severity warning
 * @example-packages benign-to-malicious-request (custom)
 * @tags ip address
 *       string consts
 */

import python
import semmle.python.dataflow.new.TaintTracking
import semmle.python.ApiGraphs

module MyFlowConfiguration implements DataFlow::ConfigSig {
  predicate isSource(DataFlow::Node source) {
    exists(StrConst c |
      (c.toString()
          .regexpMatch(".*(([0-9]|[1-9][0-9]|1[0-9]{2}|2[0-4][0-9]|25[0-5])\\.){3}([0-9]|[1-9][0-9]|1[0-9]{2}|2[0-4][0-9]|25[0-5]).*")
      or
      c.getText()
          .regexpMatch(".*(([0-9]|[1-9][0-9]|1[0-9]{2}|2[0-4][0-9]|25[0-5])\\.){3}([0-9]|[1-9][0-9]|1[0-9]{2}|2[0-4][0-9]|25[0-5]).*")
    ) and
     not c.toString().matches(["%127.0.0.1%"])
     and
     not c.toString().matches(["%0.0.0.0%"])
     and
     not c.toString().matches(["%8.8.8.8%"])

    |
      source.asCfgNode() = c.getAFlowNode()
    )
  }

  predicate isSink(DataFlow::Node sink) {
    (
      sink = API::moduleImport("socket").getMember(_).getACall() or
      sink = API::moduleImport("requests").getMember(_).getACall() or
      sink = API::moduleImport("urllib3").getMember(_).getACall() or
      sink.(DataFlow::CallCfgNode)
          .getFunction()
          .toString()
          .regexpMatch(".*(write|sendall|send|post|put|patch|delete|get|exec|eval|dumps|system?).*") or
      sink.(DataFlow::MethodCallNode)
          .getMethodName()
          .regexpMatch(".*(write|sendall|send|post|put|patch|delete|get|exec|eval|dumps|system?).*")
    ) and
    not sink.getLocation().getFile().inStdlib()
  }

  predicate isAdditionalFlowStep(DataFlow::Node nodeFrom, DataFlow::Node nodeTo) {
    exists(DataFlow::ExprNode expr | expr = nodeTo |
      expr.asCfgNode().getAChild() = nodeFrom.asCfgNode()
    )
    or
    exists(DataFlow::CallCfgNode call | call = nodeTo |
      call.getArgByName(_) = nodeFrom or
      call.getArg(_) = nodeFrom
    )
    or
    TaintTracking::localTaint(nodeFrom, nodeTo)
  }
}

module MyFlow = DataFlow::Global<MyFlowConfiguration>;

// Get all string concats
from StrConst c, DataFlow::Node source, DataFlow::Node sink
// Detect most common tlds
where
  source.asCfgNode() = c.getAFlowNode() and
  MyFlowConfiguration::isSource(source) and
  MyFlowConfiguration::isSink(sink) and
  MyFlow::flow(source, sink) and
  source != sink
select source,
  "Detected FLOW of IP: " + c.getText() + " , from " + source + " at " + source.getLocation() +
    " to " + sink + " at " + sink.getLocation()
