/**
 * @name Detects const domain names
 * @description Detects domain names as String constants using a regular expression which are in a dataflow
 * @Author Martin Mirchev
 * @kind problem
 * @id py/domain-flow-name-const
 * @security-severity 4.0
 * @problem.severity warning
 * @example-packages benign-to-malicious-request (custom)
 * @tags domain name
 *       string consts
 */

import python
import semmle.python.dataflow.new.TaintTracking
import semmle.python.ApiGraphs

module MyFlowConfiguration implements DataFlow::ConfigSig {
  predicate isSource(DataFlow::Node source) {
    exists(StrConst c |
      c.toString()
          .regexpMatch("(?i)[^@]*\\.(com|net|org|jp|de|uk|fr|br|it|ru|es|me|gov|pl|ca|au|cn|co|" +
              "in|nl|edu|info|eu|ch|id|at|kr|cz|mx|be|tv|se|tr|tw|al|ua|ir|vn|cl|sk|ly|cc|to|no|fi|us|pt|dk|ar|hu|tk|"
              +
              "gr|il|news|ro|my|biz|ie|za|nz|sg|ee|th|io|xyz|pe|bg|hk|rs|lt|link|ph|club|si|site|mobi|by|cat|wiki|la|"
              + "ga|xxx|cf|hr|ng|jobs|online|kz|ug|gq|ae|is|lv|pro|fm|tips|ms|sa|app)(/.*)?")
      or
      c.getText()
          .regexpMatch("(?i)[^@]*\\.(com|net|org|jp|de|uk|fr|br|it|ru|es|me|gov|pl|ca|au|cn|co|" +
              "in|nl|edu|info|eu|ch|id|at|kr|cz|mx|be|tv|se|tr|tw|al|ua|ir|vn|cl|sk|ly|cc|to|no|fi|us|pt|dk|ar|hu|tk|"
              +
              "gr|il|news|ro|my|biz|ie|za|nz|sg|ee|th|io|xyz|pe|bg|hk|rs|lt|link|ph|club|si|site|mobi|by|cat|wiki|la|"
              + "ga|xxx|cf|hr|ng|jobs|online|kz|ug|gq|ae|is|lv|pro|fm|tips|ms|sa|app)(/.*)?")
    |
      source.asCfgNode() = c.getAFlowNode()
    )
  }

  predicate isSink(DataFlow::Node sink) {
    (
      sink = API::moduleImport("socket").getMember(_).getACall() or
      sink = API::moduleImport("requests").getMember(_).getACall() or
      sink = API::moduleImport("urllib3").getMember(_).getACall() or
      sink = API::moduleImport("httpx").getAMember().getACall() or
      sink.(DataFlow::CallCfgNode)
          .getFunction()
          .toString()
          .regexpMatch(".*(request|sendall|connect|urlretrieve|urlopen|send|exec|eval|system?).*") or
      sink.(DataFlow::MethodCallNode)
          .getMethodName()
          .regexpMatch(".*(request|sendall|connect|urlretrieve|urlopen|send|exec|eval|system?).*")
    ) and
    not sink.getLocation().getFile().inStdlib()
  }

  predicate isAdditionalFlowStep(DataFlow::Node nodeFrom, DataFlow::Node nodeTo) {
    exists(DataFlow::ExprNode expr | expr = nodeTo |
      expr.asCfgNode().getAChild() = nodeFrom.asCfgNode()
    )
    or
    exists(DataFlow::CallCfgNode call | call = nodeTo |
      call.getArgByName(_) = nodeFrom or
      call.getArg(_) = nodeFrom
    )
    or
    TaintTracking::localTaint(nodeFrom, nodeTo)
  }
}

module MyFlow = DataFlow::Global<MyFlowConfiguration>;

// Get all string concats
from StrConst c, DataFlow::Node source, DataFlow::Node sink
// Detect most common tlds
where
  source.asCfgNode() = c.getAFlowNode() and
  MyFlowConfiguration::isSource(source) and
  MyFlowConfiguration::isSink(sink) and
  MyFlow::flow(source, sink) and
  source != sink
select source,
  "Detected FLOW of URL: " + c.getText() + " , from "  + source.getLocation() +
    " to " + sink + " at " + sink.getLocation()
